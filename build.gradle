buildscript {
    // To fetch deps from mobi's nexus or publish to nexus
    ext {
        def console = System.console()
        if (!project.hasProperty("nexusUser") && console) {
            ext.nexusUser = console.readLine("\n\n> Mobi Nexus Username: ").toString().trim()
        }
        if (!project.hasProperty("nexusPass") && console) {
            ext.nexusPass = console.readPassword("\n\n> Mobi Nexus Password: ").toString().trim()
        }
    }

    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.google.gms:google-services:3.0.0'
        classpath 'com.android.tools.build:gradle:2.2.2'
        classpath 'me.tatarka.retrolambda.projectlombok:lombok.ast:0.2.3.a2'
    }

    // Exclude the version that the android plugin depends on.
    configurations.classpath.exclude group: 'com.android.tools.external.lombok'
}

ext {
    versionName = "0.0.1"
    versionCode = 1
    isRelease = false
}

def getDate() {
    def date = new Date()
    def formattedDate = date.format('yyyy.MM.dd')
    return formattedDate
}


allprojects {
    buildscript {
        project.group = "mobi"
        // Yes yes, I am cheating here.
        project.ext.isRelease = project.gradle.startParameter.taskNames.contains("buildGreenhouseRelease")

        String suppliedBuildNumber = "$System.env.BUILD_NUMBER"
        // VersionName
        if (suppliedBuildNumber == null || suppliedBuildNumber.equals("") || suppliedBuildNumber.equals("null")) {
            project.ext.versionName = getDate() + (isRelease ? ".r" : ".d") + "1"
        } else {
            project.ext.versionName = getDate() + (isRelease ? ".r" : ".d") + suppliedBuildNumber
        }
        // This is the synonym
        project.version = project.ext.versionName

        // VersionCode
        if (suppliedBuildNumber == null || suppliedBuildNumber.equals("") || suppliedBuildNumber.equals("null")) {
            project.ext.versionCode = 1
        } else {
            project.ext.versionCode = Integer.valueOf(suppliedBuildNumber)
        }
    }
    repositories {
        jcenter()
        flatDir {
            dirs 'libs'
        }
        ivy {
            credentials {
                username project.nexusUser
                password project.nexusPass
            }
            url "https://nexus.lab.mobi/content/repositories/mobi-libs/"
            ivyPattern "https://nexus.lab.mobi/content/repositories/mobi-libs/[organisation]/[module](/[branch])/[revision]/ivy.xml"
            artifactPattern "https://nexus.lab.mobi/content/repositories/mobi-libs/[organisation]/[module](/[branch])/[revision]/[artifact].[ext]"
        }
    }
}
